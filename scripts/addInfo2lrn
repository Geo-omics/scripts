#!/usr/bin/perl

# Copyright 2013, 2014, 2019 Regents of The University of Michigan.

# This file is part of geo-omics-scripts.

# Geo-omics-scripts is free software: you can redistribute it and/or
# modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.

# Geo-omics-scripts is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.

# You should have received a copy of the GNU General Public License along
# with Geo-omics-scripts.  If not, see <https://www.gnu.org/licenses/>.


=head1 NAME

addInfo2lrn - adds additional columns of data to LRN files


=head1 SYNOPSIS

B<addInfo2lrn> B<-info> scaffolds.info B<-lrn> esom.lrn B<-names> esom.names B<-out> file_with_additional_data.lrn


=head1 DESCRIPTION

Adds additional columns of data to the LRN files. You may want to add data like bin coverage, GC etc.


=head1 OPTIONS

=over 8

=item B<-info> I<CHAR>

file with additional data; FORMAT: Scaffold_Name<TAB>Feature1<TAB>Feature2<TAB>...<TAB>FeatureN

=item B<-lrn> I<CHAR>

LRN file created from the ESOM wrapper script.

=item B<-names> I<CHAR>

NAMES file created from the ESOM wrapper script.

=item B<-out> I<CHAR>

output file in the *.lrn format with the additional columns.

=item B<-map> I<CHAR>

If your contig/scaffold names were changed at some point such that the coverage names don;t match your scaffold names anymore, create a file in the format:

=item B<scaffold_name_asIn_Coverage> I<TAB>

scaffold_name_asIn_ESOM_input_fasta

=item B<-version>, B<-v> I<BOOLEAN>

version of the current script

=item B<-help>, B<-h> I<BOOLEAN>

This message.

=back


=head1 AUTHOR

Sunit Jain, (Mon Nov 18 11:34:44 EST 2013)
sunitj [AT] umich [DOT] edu


=head1 SEE ALSO

L<omics(1)>, L<illumina-reads-processing(7)>

=head2 Other local resources

=over

=item [1]

L<HTML documentation|file:///usr/share/doc/geo-omics-scripts/html/index.html>

=item [2]

L<Omics workflow documentation [PDF]|file:///usr/share/doc/geo-omics-scripts/Geomicro-Illumina-Reads-Processing-Pipeline.pdf>

=back

=head2 Web

=over

=item [3]

L<Workflow documentation [PDF]|https://drive.google.com/open?id=0BxFSivK8RfJed05wamtrbEVUeE0>

=item [4]

L<Website|http://www.earth.lsa.umich.edu/geomicrobiology/>

=item [5]

L<Github repository|https://github.com/Geo-omics/scripts>

=back

=cut

use strict;
use Getopt::Long;

my $help;
my $version="addInfo2lrn\tv0.0.4";
my ($info, $lrn, $names, $out, $map);
GetOptions(
	'map:s'=>\$map,
	'info:s'=>\$info,
	'lrn:s'=>\$lrn,
	'names:s'=>\$names,
	'out|o:s'=>\$out,
	'v|version'=>sub{print $version."\n"; exit;},
	'h|help'=>sub{system("perldoc $0 \| cat"); exit;},
);
print "\# $version\n";

my %alias;
if($map){
	open(MAP, "<".$map)|| die $!;
	while(my $line=<MAP>){
		next if ($line=~ /#/);
		chomp $line;
		next unless $line;

		my($original, $JGI)=split(/\t/, $line);

		$alias{uc($original)}=$JGI;
	}
	close MAP;
}

open(INFO, "<".$info) || die $!;
my (%INFO,@newHeaders);
while(my $line=<INFO>){
	chomp $line;
	next unless $line;
	my ($name, @data)=split(/\t/, $line);

	if (($.==1) && ($line=~ /^#/)){
		push(@newHeaders, @data);
		next;
	}
	elsif($.==1){
		for(my $i=1; $i <= scalar(@data); $i++){
			push(@newHeaders, "Feature".$i);
		}
	}
	
	$name=$alias{uc($name)} if ($map);

	$INFO{uc($name)}=join("\t", @data);
}
close INFO;

open(NAMES, "<".$names) || die $!;
my %NAMES;
while(my $line=<NAMES>){
	chomp $line;
	next unless $line;
	next if ($line=~ /^#/);
	next if ($line=~ /^%/);
	
	my ($num, $ann, $contig)=split(/\t/, $line);
	
	if ($INFO{uc($contig)}){
		$NAMES{uc($num)}=$INFO{uc($contig)};
	}
}
close NAMES;
undef %INFO;

open(LRN, "<".$lrn)|| die $!;
open(OUT, ">".$out)|| die $!;
while(my $line=<LRN>){
	chomp $line;
	next unless $line;
	next if ($line=~ /^#/);
	
	my $addCols=scalar(@newHeaders);
	if ($line=~ /^\%/){
		if($.==1){
			print OUT $line."\n";
		}
		if($.==2){
			$line=~ s/\%//;
			print OUT "%".($line+$addCols)."\n";
		}
		if($.==3){
			for(my $i=0; $i < $addCols; $i++){
				$line.="\t1";
			}
			print OUT $line."\n";
		}
		if($.==4){
			for(my $i=0; $i < $addCols; $i++){
				$line.="\t".$newHeaders[$i];
			}
			print OUT $line."\n";
		}
	}
	else{
		my($num, @data)=split(/\t/, $line);
		if($NAMES{$num}){
			$line.="\t".$NAMES{$num};
		}
		else{
			$line.="\t".(join("\t",((0) x $addCols)));
		}
		print OUT $line."\n";
	}
}
close LRN;
close OUT;

exit 0;

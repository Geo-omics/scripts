#!/usr/bin/perl

# Copyright 2013, 2019 Regents of The University of Michigan.

# This file is part of geo-omics-scripts.

# Geo-omics-scripts is free software: you can redistribute it and/or
# modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.

# Geo-omics-scripts is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.

# You should have received a copy of the GNU General Public License along
# with Geo-omics-scripts.  If not, see <https://www.gnu.org/licenses/>.


=head1 NAME

findStretchesOfNs - find streaches of N in fasta file

=head1 SYNOPSIS

B<findStretchesOfNs> -f infile.fasta B<-coord> outfile.tsv [OPTIONS]


=head1 OPTIONS

=over 8

=item B<-nuc> I<character>

any character other than 'N'	(default: N)

=item B<-coord> I<character>

outputs coordinates file, start and stop position of the stretch of N's

=item B<-min> I<integer>

minimum stretch of N's	(default: 10)

=item B<-max> I<integer>

maximum number of N's	(default: 500)

=item B<-split> I<character>

output file; split the fasta sequence at N's and write to this file.

=item B<-minLen> I<integer>

When splitting on Ns, what's the smallest sequence you wish to obtain. (Default: 100 (bases))

=back


=head1 EXAMPLE

B<findStretchesOfNs> -f test.fasta -o test.coord B<-min> 1 B<-split> test_split_l20.fasta -len 20


=head1 AUTHOR

Sunit Jain, January 2013
sunitj [AT] umich [DOT] edu


=head1 SEE ALSO

L<omics(1)>, L<illumina-reads-processing(7)>

=head2 Other local resources

=over

=item [1]

L<HTML documentation|file:///usr/share/doc/geo-omics-scripts/html/index.html>

=item [2]

L<Omics workflow documentation [PDF]|file:///usr/share/doc/geo-omics-scripts/Geomicro-Illumina-Reads-Processing-Pipeline.pdf>

=back

=head2 Web

=over

=item [3]

L<Workflow documentation [PDF]|https://drive.google.com/open?id=0BxFSivK8RfJed05wamtrbEVUeE0>

=item [4]

L<Website|http://www.earth.lsa.umich.edu/geomicrobiology/>

=item [5]

L<Github repository|https://github.com/Geo-omics/scripts>

=back

=cut

use strict;
use Getopt::Long;

my $version="v0.3.7";
my $fasta;
my $coord;
my $nuc="N";
my $min=10;
my $max=500;
my $split;
my $minLen=100;
GetOptions(
	"f|fasta:s"=>\$fasta,
	"c|o|coord:s"=>\$coord,
	"n|nuc:s"=>\$nuc,
	"split:s"=>\$split,
	"min:i"=>\$min,
	"max:i"=>\$max,
	"len:i"=>\$minLen,
	"h|help"=>sub{system('perldoc', $0); exit;},
);
print "# findStretchesOfNs $version\n";
die "[ERROR] Choose an output type, either '-coord' or '-split'. See '-h' for more options.\n" if(! $coord) && (! $split);

my $find=quotemeta "$nuc";
open(FASTA, $fasta)|| die $!;
if ($coord){
	open(OUT, ">".$coord)|| die $!;
	print OUT "# Sequence_Name\tN_Start\tN_Stop\tN_Length\n";
}

if ($split){
	open(SPLIT, ">".$split)|| die $!;
}
my %uniqueSeqNames;
$/=">";
while(my $line=<FASTA>){
	chomp $line;
	next unless $line;

	my($header, @sequence)=split("\n", $line);
	my $seq=uc(join("",@sequence));
	my ($realHeader, @description)=split(/\s+/, $header);
	my $desc=join(" ", @description);
	my $parts=0;
	my $lastOffset=0;
	while($seq=~ /($find){$min,$max}/ig){
		print OUT $header."\t".$-[0]."\t".$+[0]."\t".($+[0]-$-[0])."\n";
		if($split){
			my $currentOffset=$-[0];
			print "Last Offset:\t$lastOffset\tCurrent Offset:\t$currentOffset\n";
			my $subSeq = substr($seq, $lastOffset, ($currentOffset - $lastOffset));
			if(length($subSeq) >= $minLen){
				print SPLIT ">".$realHeader."_".$parts." ".$desc."\n";
				print SPLIT $subSeq."\n";
				$parts++;
			}
			$lastOffset=$+[0];
		}
		$uniqueSeqNames{$header}++;
	}
	print SPLIT ">".$realHeader."_".$parts." ".$desc."\n";
	print SPLIT substr($seq, $lastOffset)."\n";
}
$/="\n";
close FASTA;
close OUT if($coord);
close SPLIT if($split);
print "# Number of Sequence with at least 1 stretch of Ns >= $min:\t".keys(%uniqueSeqNames)."\n";

#!/usr/bin/perl

=head1 NAME

nameClassFiles - Rename class files according to a tab-separated list of old/original names to new/more sensible names.


=head1 SYNOPSIS

B<nameClassFiles> B<-tsv> old_and_new_filenames.tsv B<-ext> fasta


=head1 DESCRIPTION

Rename class files according to a tab-separated list of old/original names to new/more sensible names.


=head1 OPTIONS

=over 8

=item B<-tsv> I<CHAR>

col1=old name; <TAB> col2=new name

=item B<-out> I<CHAR>

Output Folder [default: "Renamed"]

=item B<-ext> I<CHAR>

extensions for the old and new names if the tsv doesn't already have them.

=item B<-version>, B<-v> I<BOOLEAN>

version of the current script

=item B<-help>, B<-h> I<BOOLEAN>

This message.

=back


=head1 AUTHOR

Sunit Jain, (Mon Feb 10 14:26:12 EST 2014)
sunitj [AT] umich [DOT] edu


=head1 LICENSE

This script is licensed under a Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.


=head1 DISCLAIMER

This script is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.


=cut

use strict;
use Getopt::Long;
use File::Spec;
use File::Copy "cp";

my ($tsv);
my $out="Renamed";
my $ext="fasta";
my $help;
my $version="nameClassFiles.pl\tv0.0.2b";
GetOptions(
	'list|tsv:s'=>\$tsv,
	'out:s'=>\$out,
	'ext:s'=>\$ext,
	'v|version'=>sub{print $version."\n"; exit;},
	'h|help'=>sub{system("perldoc $0 \| cat"); exit;},
);
print "\# $version\n";

unless (-e $out){mkdir($out, 0755)};

my %tracker;
open(TSV, "<".$tsv)|| die $!;
while(my $line=<TSV>){
	next if ($line=~ "^#");
	chomp $line;
	next unless $line;
	
	my($old, $new)=split(/\t/, $line);
	if ($ext){
		$old.=".".$ext;
		$new.=".".$ext;
	}
	$tracker{$old}=$new;
}
close TSV;

foreach my $file(keys %tracker){
	my $new=File::Spec->catfile( $out, $tracker{$file} );
	print "Creating:\t$new\n";
	cp($file, $new);
}

#!/usr/bin/perl

=head1 NAME

top5 - This program gets the top 5 hits for each query in a blast tabular output file.


=head1 SYNOPSIS

B<top5> B<-b> <blast output> B<-t> <top X hits; default: 5> B<-o> <output_File_Name>


=head1 DESCRIPTION

This program gets the top 5 hits for each query in a blast tabular output file.


=head1 OPTIONS

=over 8

=item B<-b> I<CHAR>

Blast output in tabular (-outfmt 6/7 OR -m 8/9). [REQUIRED]

=item B<-t> I<INT>

# of top hits. [default = 5 ]

=item B<-o> I<CHAR>

output file; same format as blast file

=item B<-no_self> I<BOOLEAN>

remove self hits. helpful in a self blast.

=item B<-version>, B<-v> I<BOOLEAN>

version of the current script

=item B<-help>, B<-h> I<BOOLEAN>

This message.

=back


=head1 EXAMPLE

B<top5> B<-b> sample.blastn B<-o> sample_topHits.blastn B<-t> 1


=head1 AUTHOR

Sunit Jain, (Tue May 13 16:07:06 EDT 2011)
sunitj [AT] umich [DOT] edu


=head1 LICENSE

This script is licensed under a Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.


=head1 DISCLAIMER

This script is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.


=cut

use strict;
use Getopt::Long;

my $in;
my $x=5;
my $out=$$.".top5";
my $no_self;
my $version="top5.pl\tv0.1.1";
GetOptions(
	'b:s'=>\$in,
	't:i'=>\$x,
	'o|out:s'=>\$out,
	'no_self'=>\$no_self,
	'h'=> sub{system("perldoc $0 \| cat"); exit;},
);

open (TB, $in) || die "[err] $in: $! \n";
my %file;
while (my $line=<TB>){
	next if ($line=~ m/^#/);
	chomp $line;
	$line=~ s/\r//;
	next unless $line;
	
	my($query, @etc)=split(/\t/, $line);
	if($no_self){
		my ($subj, @desc)=split(/ /, $etc[0]);
		next if $query eq $subj;
	}
	
	push(@{$file{$query}}, $line);
}
close TB;

my($nothing, $fileName)=split(/\_/, $in);
open (OUT, ">".$out)|| die "[err] $out: $! \n";;
while(my($k,$val)= each(%file)){
	my $count=0;
	foreach my $v(@{$val}){
		if ($count < $x){
			print OUT $v."\n";
		}
		$count++;
	}
}
close OUT;

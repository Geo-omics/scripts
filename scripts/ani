#!/usr/bin/perl

# Copyright 2019 Regents of The University of Michigan.

# This file is part of geo-omics-scripts.

# Geo-omics-scripts is free software: you can redistribute it and/or
# modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.

# Geo-omics-scripts is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.

# You should have received a copy of the GNU General Public License along
# with Geo-omics-scripts.  If not, see <https://www.gnu.org/licenses/>.


=head1 NAME

ani - calculates Average Nucleotide Identity (ANI) for a pair of bacterial genomes.


=head1 SYNOPSIS

B<ani> B<-blast> I<path to blast binaries> -B<query> I<genome> -B<subj> I<the other strain genome >-B<outdir> I<output directory>


=head1 DESCRIPTION

This program calculates Average Nucleotide Identity (ANI) for a pair of bacterial genomes.
NOTE: Requires an older version of blast. Tested on version 2.2.20


=head1 OPTIONS

=over 8

=item B<query> I<CHAR>

Query strain genome sequence in FASTA format. [REQUIRED]

=item B<subj> I<CHAR>

Subject strain genome sequence in FASTA format. [REQUIRED]

=item B<outdir> I<CHAR>

output directory

=item B<threads> I<INT>

number of processors to use for blast

=item B<-version>, B<-v> I<BOOLEAN>

version of the current script

=item B<-help>, B<-h> I<BOOLEAN>

This message.

=back


=head1 EXAMPLE

B<ani> B<-blast> ./blast-2.2.20/bin/ -B<query> strain1.fa -B<subj> strain2.fa -B<outdir> result


=head1 AUTHOR

Sunit Jain, (Tue May 13 16:07:06 EDT 2014)
sunitj [AT] umich [DOT] edu


=head1 LICENSE

This script is licensed under a Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.


=head1 DISCLAIMER

This script is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.


=head1 SEE ALSO

L<omics(1)>, L<illumina-reads-processing(7)>

=head2 Other local resources

=over

=item [1]

L<HTML documentation|file:///usr/share/doc/geo-omics-scripts/html/index.html>

=item [2]

L<Omics workflow documentation [PDF]|file:///usr/share/doc/geo-omics-scripts/Geomicro-Illumina-Reads-Processing-Pipeline.pdf>

=back

=head2 Web

=over

=item [3]

L<Workflow documentation [PDF]|https://drive.google.com/open?id=0BxFSivK8RfJed05wamtrbEVUeE0>

=item [4]

L<Website|http://www.earth.lsa.umich.edu/geomicrobiology/>

=item [5]

L<Github repository|https://github.com/Geo-omics/scripts>

=back

=cut

use strict;
use Getopt::Long;
use strict;
use List::Util qw(min);
use Cwd qw(abs_path);

my $help;
my $version="ani\tv0.0.3b";
my ($q, $s, $outDir, $path_to_blast_bin, $num_threads);
GetOptions(
    "query:s" => \$q,
    "subj:s" => \$s,
    "o|outdir:s" => \$outDir,
    "t|threads:s"=>\$num_threads,
    'v|version'=>sub{print $version."\n"; exit;},
    'h|help'=>sub{system("perldoc $0 \| cat"); exit;},
);
print "\# $version\n";

if(! $q || ! $s){ system("perldoc $0 \| cat"); exit; }

my $query=abs_path($q);
my $subj=abs_path($s);
$outDir=$q."_vs_".$s if (! $outDir);
unless(-d $outDir){`mkdir $outDir`;}
$num_threads=4 if ( ! $num_threads);

#Split query genome and write segments in $outDir/Query.split
my $chop_len = 1020; 
$/ = "\n>";
open (QUERY, "<".$query) || die "$query $!\n";
open (CHOP,">".$outDir."/Query.split") || die "$outDir/Query.Split";
while(<QUERY>){
    chomp;
    s/>//g;
    my ($scaf,$seq) = (split /\n/,$_,2);
    my $scaf_name = (split /\s+/,$scaf)[0];
    $seq =~ s/\s+//g;
    my @cut = ($seq =~ /(.{1,$chop_len})/g);
    for my $cut_num (0..$#cut){
        next if length($cut[$cut_num]) < 100; 
        my $sgmID = "$scaf_name\_$cut_num";
        print CHOP ">$sgmID\n$cut[$cut_num]\n";
    }
}
close QUERY;
close CHOP;
$/ = "\n";

#BLAST alignment
`ln -sf $subj $outDir/Subject.fa`;
`formatdb -i $outDir/Subject.fa -p F`;
#`makeblastdb -dbtype nucl -in $outDir/Subject.fa`;
#`blastn -task blastn -db $outDir/Subject.fa -query $outDir/Query.split -evalue 1e-15 -xdrop_gap 150 -num_threads $num_threads -penalty -1 -out $outDir/raw.blast -outfmt 6 -dust no`;
`blastall -i $outDir/Query.split -d $outDir/Subject.fa -X 150 -q -1 -F F -e 1e-15 -m 8 -a $num_threads -o $outDir/raw.blast -p blastn`;

#Set Identity and Alignment Percentage cut off following paper of JSpecies
my $pid = 30;
my $cvg_cut = 70;

my ($ANI,%query_best,$sumID,$count);
open (BLAST,"<".$outDir."/raw.blast") || die "raw.blast $!\n";
open (ANI, ">".$outDir."/ani_raw.blast") || die "ani_raw.blast $!\n";
while(<BLAST>){
    chomp;
    my @t = split;
    next if $t[3] < 100;
    next if exists $query_best{$t[0]}; 
    $query_best{$t[0]} = 1; #only use best hit for every query segments
    next if $t[2]<=$pid;
    next if ($t[3]*100/1020) < $cvg_cut;
    print ANI $t[0]."_".$q."\t".$t[2]."\t".$q."_vs_".$s."\n";
    $sumID += $t[2];
    $count++;
}
close BLAST;
close ANI;
$ANI = $sumID/$count;
print "$q\t$s\t$ANI\n";
